#SYMFILE "@:PEDES.SYM"
##
#AUTONUM 1
#CONTROLCODES 1

BANK 1

REM PETSCIIPEDE VARIABLES
REM -------------------
JNUM=1:J=0:B=0
GAMESTATE=0
MISSILEX=0:MISSILEY=0:MISSILECOLLIDECHAR=0:MISSILECHAR=$5D
PLAYERX=0:PLAYERY=0
CURRENTCOLORSCHEME=0
PEDELEN=0:PEDELENMAX=12:PEDEDELAY=0:PEDEDELAYCOUNT=0
PLAYERCHAR=90:PLAYERCOLOR=$50
FRAMECOLOR=$50
ENEMYBODYCHAR=81:ENEMYHEADCHAR=83:MUSHROOMCOLOR=$73:EXPLODECHAR=$56
MUSHROOMCHAR=88:SPIDERCHAR=$55:FLEACHAR=$5E:EXPLODECOLOR=$34
PLAYERDELAYCOUNT=0:PLAYERDELAY=2
ENEMYX=$A000:ENEMYY=ENEMYX+16
ENEMYBODY=ENEMYY+16:ENEMYDIR=ENEMYBODY+16
ENEMYROUTE=ENEMYDIR+16
ENEMYTOXIC=0
NORMALSPEED=0:NUMBER1=0:ALPHA1$="":HITS=0:SPECIALSDELAY=7
SCORE=0:HIGHSCORE=0:WAVESCORE=0:POINTS=0:NEWLIFESCORE=1000
CHARROM=$F000

REM - ASSIGN COLOR SCHEMES
COLORSPEDE(0)=$DC:COLORSMUSHROOMS(0)=$73
COLORSPEDE(1)=$8F:COLORSMUSHROOMS(1)=$FF
COLORSPEDE(2)=$A4:COLORSMUSHROOMS(2)=$34
COLORSPEDE(3)=$E3:COLORSMUSHROOMS(3)=$A3
COLORSPEDE(4)=$0E:COLORSMUSHROOMS(4)=$57

REM ---------------------------------------------
REM -MAKE SCREEN 32X25 WITH 256 COLOR CHARACTERS
REM ---------------------------------------------
COLOR 1,0:SCREEN 11:POKE $9F34,PEEK($9F34) OR 8
POKE $9F2C,0:CLS
PSGINIT:PSGVOL 1,63:PSGVOL 3,63:PSGWAV 3,63
REM - LOAD CUSTOM CHARACTER SET AND ML PORTION OF GAME
GOSUB LOADCUSTOMCHARS:GOSUB LOADML

REM ---------------------------------------------
REM - START GAME
REM ---------------------------------------------
STARTGAME:
    GOSUB SPLASH:LIVES=3:SCORE=0:LEVEL=1:PEDELEN=6:PEDEDELAY=6:NORMALDELAY=PEDEDELAY



REM ---------------------------------------------
REM - START LEVEL
REM ---------------------------------------------
STARTLEVEL:
    GOSUB DRAWHUD
    REM ZERO OUT HI MEM WHERE PETSCIIPEDE INFO IS STORED
    POKE $3C,PEDELEN:GOSUB INITPEDES
    REM SET THE INITIAL LENGTH OF THE PETSCIIPEDE    
    POKE $3E,COLORSPEDE(CURRENTCOLORSCHEME):POKE $3F,COLORSMUSHROOMS(CURRENTCOLORSCHEME)
    REM ADD MUSHROOMS
    GOSUB ADDMUSHROOMS
    REM START LEVEL SOUND
    FOR S=1500 TO 20 STEP -5:PSGFREQ 0,S*8:PSGFREQ 0,S:NEXT:PSGFREQ 0,0
    REM SET PLAYER START POSITION
    POKE $26,15:POKE $27,22:SYS $0409
    REM CLEAR GAMESTATE
    POKE $23,0:POKE $24,0:POKE $25,0:POKE $40,0:POKE $39,0:POKE $40,20:HITS=0
    WAVESCORE=0
    REM SET DELAY FOR SPECIAL ENEMIES AND TURN OFF TOXIC FLAG
    POKE $37,SPECIALSDELAY:POKE $38,0:POKE $39,0
    REM RESET CLOCK
    TI=0



REM ---------------------------------------------
REM 
REM - GAME LOOP
REM 
REM ---------------------------------------------
GAMELOOP:
    MISSILEACTIVE=PEEK($23):SPIDERACTIVE=PEEK($24):FLEAACTIVE=PEEK($25):ENEMYTOXIC=PEEK($39)
    PLAYERDELAYCOUNT=PLAYERDELAYCOUNT+1
    J=JOY(JNUM)
    IF J=$10 THEN ENDGAME
    IF J AND 2 THEN GOSUB MOVEPLAYERLEFT
    IF J AND 1 THEN GOSUB MOVEPLAYERRIGHT
    IF J AND 8 THEN GOSUB MOVEPLAYERUP
    IF J AND 4 THEN GOSUB MOVEPLAYERDOWN
    IF J>$39 AND MISSILEACTIVE=0 THEN GOSUB FIREMISSILE

    REM CHECK DELAY FOR PEDE UPDATE
    PEDEDELAYCOUNT=PEDEDELAYCOUNT+1:IF PEDEDELAYCOUNT>=PEDEDELAY THEN PEDEDELAYCOUNT=0:SYS $0403:SYS $0406

    REM HANDLE RUNNING INTO TOXIC MUSHROOM
    IF ENEMYTOXIC>0 THEN PEDEDELAY=0
    REM SECONDARY ENEMY
    IF LEVEL>2 AND TI>400 THEN GOSUB SPAWNENEMY

    REM UPDATE MISSILE,SPIDER AND/OR FLEA
    SYS $040F:MISSILEACTIVE=PEEK($23)

    REM UPDATE MISSILE    
    IF MISSILEACTIVE=1 THEN MISSILECOLLIDECHAR=PEEK($2F):IF MISSILECOLLIDECHAR<>32 THEN GOSUB MISSILECOLLIDE

    REM GET THE CURRENT GAME STATE
    GAMESTATE=PEEK($40)

    REM CHECK GAME STATE FOR WON/LOST/GAME OVER
    IF GAMESTATE=$10 THEN LEVELWON
    IF GAMESTATE=$11 THEN LEVELLOST
    IF GAMESTATE=$FF THEN ENDGAME

    GOTO GAMELOOP



REM -------------------------------------
REM -
REM - MOVE PLAYER 
REM -
REM -------------------------------------
MOVEPLAYERLEFT:
    IF PLAYERDELAYCOUNT<PLAYERDELAY THEN RETURN
    PLAYERDELAYCOUNT=0
    PLAYERX=PEEK($26):PLAYERY=PEEK($27)
    IF PLAYERX<2 THEN RETURN    
    IF TDATA(PLAYERX-1,PLAYERY)<>32 THEN RETURN
    SYS $040C:REM TILE PLAYERX,PLAYERY,32
    PLAYERX=PLAYERX-1:POKE $26,PLAYERX
    SYS $0409:REM TILE PLAYERX,PLAYERY,PLAYERCHAR,PLAYERCOLOR
    RETURN    


MOVEPLAYERRIGHT:
    IF PLAYERDELAYCOUNT<PLAYERDELAY THEN RETURN
    PLAYERDELAYCOUNT=0
    PLAYERX=PEEK($26):PLAYERY=PEEK($27)
    IF PLAYERX>29 THEN RETURN
    IF TDATA(PLAYERX+1,PLAYERY)<>32 THEN RETURN
    SYS $040C:REM TILE PLAYERX,PLAYERY,32
    PLAYERX=PLAYERX+1:POKE $26,PLAYERX
    SYS $0409:REM TILE PLAYERX,PLAYERY,PLAYERCHAR,PLAYERCOLOR
    RETURN


MOVEPLAYERUP:
    IF PLAYERDELAYCOUNT<PLAYERDELAY THEN RETURN
    PLAYERDELAYCOUNT=0
    PLAYERX=PEEK($26):PLAYERY=PEEK($27)
    IF PLAYERY<20 THEN RETURN
    IF TDATA(PLAYERX,PLAYERY-1)<>32 THEN RETURN
    SYS $040C:REM HIDE PLAYER
    PLAYERY=PLAYERY-1:POKE $27,PLAYERY
    SYS $0409:REM SHOW PLAYER
    RETURN


MOVEPLAYERDOWN:
    IF PLAYERDELAYCOUNT<PLAYERDELAY THEN RETURN
    PLAYERDELAYCOUNT=0
    PLAYERX=PEEK($26):PLAYERY=PEEK($27)
    IF PLAYERY>22 THEN RETURN
    IF TDATA(PLAYERX,PLAYERY+1)<>32 THEN RETURN
    SYS $040C:REM HIDE PLAYER
    PLAYERY=PLAYERY+1:POKE $27,PLAYERY
    SYS $0409:REM SHOW PLAYER
    RETURN



REM ---------------------------------------------
REM - SPAWN SECONDARY ENENMY
REM ---------------------------------------------
SPAWNENEMY:
    IF SPIDERACTIVE=1 OR FLEAACTIVE=1 THEN TI=0:RETURN
    PSGPLAY 1,"O4L16AE":TI=0
    IF INT(RND(1)*2)+1>1 THEN GOSUB ACTIVATESPIDER:RETURN
    GOSUB ACTIVATEFLEA:RETURN


REM ---------------------------------------------
REM - ACTIVATE SPIDER
REM ---------------------------------------------
ACTIVATESPIDER:    
    POKE $24,1
    IF RIGHT$(STR$(TI),1)<"5" THEN POKE $2A,30:POKE $2C,1 OR 4:GOTO SETSPIDERY
    POKE $2A,0:POKE $2C,2 OR 4
SETSPIDERY:
    POKE $2B,20:REM INT(RND(1)*10)+17
    RETURN


REM ---------------------------------------------
REM - ACTIVATE FLEA
REM ---------------------------------------------
ACTIVATEFLEA:
    POKE $25,1
    POKE $2D,INT(RND(1)*30)+1:POKE $2E,2
    RETURN


REM ---------------------------------------------
REM - ACTIVATE THE MISSILE
REM ---------------------------------------------
FIREMISSILE:
    IF PEEK($23)>0 THEN RETURN:REM MISSILE ALREADY ACTIVE
    POKE $23,1:REM TURNS THE MISSILE ON
    POKE $28,PEEK($26):POKE $29,PEEK($27):REM SET MISSILE POS TO PLAYER POS
    POKE $2F,PLAYERCHAR:POKE $30,PLAYERCOLOR
    REM - MISSILE LAUNCH SFX
    FOR S=1500 TO 90 STEP -30:PSGFREQ 3,S:NEXT:PSGFREQ 3,0
    RETURN



REM ---------------------------------------------
REM 
REM - MISSILE COLLIDED WITH SOMETHING
REM 
REM ---------------------------------------------
MISSILECOLLIDE:
    MISSILEX=PEEK($28):MISSILEY=PEEK($29)
    IF MISSILECOLLIDECHAR=MUSHROOMCHAR OR MISSILECOLLIDECHAR=MUSHROOMCHAR+1 THEN GOSUB HITMUSHROOM:GOSUB DEACTIVATEMISSILE:RETURN
    IF MISSILECOLLIDECHAR>=ENEMYBODYCHAR AND MISSILECOLLIDECHAR<=ENEMYBODYCHAR+3 THEN GOSUB HITPEDE:RETURN
    IF MISSILECOLLIDECHAR=SPIDERCHAR THEN GOSUB HITSPIDER
    IF MISSILECOLLIDECHAR=FLEACHAR THEN GOSUB HITFLEA
    TILE MISSILEX,MISSILEY,32
    RETURN



REM ---------------------------------------------
REM 
REM - DEACTIVATE THE MISSILE
REM 
REM ---------------------------------------------
DEACTIVATEMISSILE:
    POKE $23,0:POKE $2F,32
    RETURN



REM ---------------------------------------------
REM 
REM - EXPLODE
REM 
REM ---------------------------------------------
EXPLODE:
    MISSILEX=PEEK($28):MISSILEY=PEEK($29)
    TILE MISSILEX,MISSILEY,EXPLODECHAR,EXPLODECOLOR
    GOSUB DEACTIVATEMISSILE
    RETURN



REM ---------------------------------------------
REM 
REM - A PETSCIIPEDE WAS HIT BY MISSILE
REM 
REM ---------------------------------------------
HITPEDE:        
    REM SYS $0418:REM COMPLETELY REMOVE PEDES FROM DISPLAY
    REM ------------------------
    REM - WHAT SEGMENT GOT HIT?
    REM ------------------------
    B=PEDELEN-1
    ENEMYBODYWALK:
    IF PEEK(ENEMYBODY+B)=0 THEN SKIPPART:REM SEGMENT NOT ACTIVE
    IF PEEK(ENEMYX+B)<>MISSILEX THEN SKIPPART:REM NO MISSILE HERE, NOT THE PART THAT WAS HIT 
    IF PEEK(ENEMYY+B)=MISSILEY THEN PEDEHIT:REM FOUND THE SEGMENT THAT WAS HIT BY THE MISSILE
    SKIPPART:
    B=B-1:IF B>=0 THEN ENEMYBODYWALK
    GOTO ENDHITPEDE:REM DIDN'T FIND HIT BODY PART, SHOULD NEVER HAPPEN!
    
    REM ---------------------------------------------
    REM - FOUND THE SEGMENT THAT WAS HIT, REMOVE IT
    REM ---------------------------------------------
    PEDEHIT:
    HITS=HITS+1:IF HITS>=PEDELEN THEN POKE $40,$10:REM INDICATE LEVEL WON

    REM ---------------------------------------
    REM - ERASE MISSILE POSITION AND EXPLODE
    REM ---------------------------------------
    GOSUB EXPLODE
    PSGPLAY 1,"T230O2L24BO5CO6EDG":GOSUB DEACTIVATEMISSILE:POINTS=10:GOSUB ADDPOINTS
    IF PEEK(ENEMYBODY+B)>ENEMYCHAR+1 THEN POINTS=90:GOSUB ADDPOINTS
    GOSUB DRAWSCORE:TILE MISSILEX,MISSILEY,32
    
    REM PUT MUSHROOM IN THE PLACE OF DESTROYED SEGMENT
    TILE PEEK(ENEMYX+B),PEEK(ENEMYY+B),MUSHROOMCHAR,MUSHROOMCOLOR
    REM REMOVE THE HIT BODY PART
    POKE ENEMYBODY+B,0

    REM -------------------------------------------------
    REM - IF THIS WAS A TAIL THEN DON'T SPAWN A NEW PEDE
    REM -------------------------------------------------
    IF B=PEDELEN-1 THEN ENDHITPEDE
    IF PEEK(ENEMYBODY+(B+1))=0 THEN ENDHITPEDE

    REM ------------------------------
    REM - FIND THE NEXT FORWARD HEAD
    REM ------------------------------
    FH=B-1
    CHECKFORHEAD:
    IF FH<0 THEN NOMOREHEADS
    IF PEEK(ENEMYBODY+FH)=0 THEN NOMOREHEADS
    IF PEEK(ENEMYBODY+FH)<ENEMYHEADCHAR THEN FH=FH-1:GOTO CHECKFORHEAD

    REM -----------------------------------------------    
    REM - MAKE THE SEGMENT AFTER THE ONE HIT A NEW HEAD
    REM -----------------------------------------------
    NS=B+1:IF NS>=PEDELEN THEN ENDHITPEDE
    POKE ENEMYDIR+NS,PEEK(ENEMYDIR+FH):POKE ENEMYBODY+NS,PEEK(ENEMYBODY+FH):POKE ENEMYROUTE+NS,PEEK(ENEMYROUTE+FH):GOTO ENDHITPEDE

    NOMOREHEADS:    
    IF PEEK(ENEMYBODY+(B+1))=0 THEN ENDHITPEDE    
    REM TILE PEEK(ENEMYX+(B+1)),PEEK(ENEMYY(B+1)),32
    POKE ENEMYBODY+(B+1),ENEMYHEADCHAR+1:POKE ENEMYDIR+(B+1),2:POKE ENEMYROUTE+(B+1),PEEK(ENEMYROUTE+B)

    ENDHITPEDE:
    RETURN



REM ---------------------------------------------
REM 
REM - A MUSHROOM WAS HIT BY MISSILE
REM 
REM ---------------------------------------------
HITMUSHROOM:
    IF MISSILECOLLIDECHAR=MUSHROOMCHAR THEN TILE MISSILEX,MISSILEY,MUSHROOMCHAR+1,MUSHROOMCOLOR:RETURN
    POINTS=5:GOSUB ADDPOINTS:GOSUB DRAWSCORE:TILE MISSILEX,MISSILEY,32
    RETURN



REM ---------------------------------------------
REM 
REM - A SPIDER WAS HIT BY MISSILE
REM 
REM ---------------------------------------------
HITSPIDER:
    GOSUB EXPLODE:PSGPLAY 1,"T230O2L24BO5CO6EDG":POINTS=250:GOSUB ADDPOINTS:GOSUB DRAWSCORE:POKE $24,0
    TILE PEEK($2A),PEEK($2B),PEEK($31),PEEK($32)
    RETURN 



REM ---------------------------------------------
REM 
REM - A FLEA WAS HIT BY MISSILE
REM 
REM ---------------------------------------------
HITFLEA:
    GOSUB EXPLODE:PSGPLAY 1,"T230O2L24BO5CO6EDG":POINTS=200:GOSUB ADDPOINTS:GOSUB DRAWSCORE:POKE $25,0
    TILE PEEK($2D),PEEK($2E),PEEK($33),PEEK($34)
    RETURN



REM ---------------------------------------------
REM 
REM - REMOVE ALL ACTORS FROM SCREEN
REM 
REM ---------------------------------------------
CLEARASSETS:
    POKE $2F,32:POKE $31,32:POKE $33,32
    TILE PEEK($26),PEEK($27),32
    TILE PEEK($28),PEEK($29),32
    TILE PEEK($2A),PEEK($2B),32
    TILE PEEK($2D),PEEK($2E),32
    RETURN



REM ---------------------------------------------
REM 
REM - ADD MUSHROOMS
REM 
REM ---------------------------------------------
ADDMUSHROOMS:
    MUSHROOMCOLOR=COLORSMUSHROOMS(CURRENTCOLORSCHEME)
    FOR X=1 TO 45
        TILE INT(RND(1)*30)+1,INT(RND(1)*18)+2,MUSHROOMCHAR,MUSHROOMCOLOR
    NEXT

    RETURN



REM ---------------------------------------------
REM - LEVEL WON
REM ---------------------------------------------
LEVELWON:
    REM CYCLE THROUGH COLORS
    CURRENTCOLORSCHEME=CURRENTCOLORSCHEME+1
    IF CURRENTCOLORSCHEME>4 THEN CURRENTCOLORSCHEME=0
    REM PLAY A MOST AWESOME VICTORY SOUND
    CC=0
    FOR S=100 TO 500 STEP 2:PSGFREQ 0,S*2:PSGFREQ 0,S
    POKE $9F2C,COLORSPEDE(CC):CC=CC+.03:IF CC>4 THEN CC=0
    NEXT
    FOR B=0 TO 3:FOR S=300 TO 500 STEP 10:PSGFREQ 0,S*2:PSGFREQ 0,S
    POKE $9F2C,COLORSPEDE(CC):CC=CC+.03:IF CC>4 THEN CC=0
    NEXT S:NEXT B
    FOR S=1000 TO 100 STEP -5:PSGFREQ 0,S*2:PSGFREQ 0,S
    POKE $9F2C,COLORSPEDE(CC):CC=CC+.03:IF CC>4 THEN CC=0
    NEXT
    PSGFREQ 0,0:POKE $9F2C,0
    REM PROMOTE TO NEXT LEVEL
    LEVEL=LEVEL+1
    REM CHECK FOR NEW LIFE AWARDED
    GOSUB NEWLIFE
    REM GIVE PLAYER A COOL FLASHING MESSAGE
    CL=0:FLASH=0:FLASHCOUNT=0:MSGCOUNT=0
    COLOR 5,5:SCREEN 9:LOCATE 1,12:GOSUB SHOWTITLE
    REM GROW PETSCIIPEDE A LITTLE LONGER
    PEDELEN=PEDELEN+1:IF PEDELEN=PEDELENMAX THEN PEDELEN=PEDELENMAX-1
    REM SPEED UP THE PETSCIIPEDE 
    PEDEDELAY=NORMALDELAY:IF LEVEL/2=INT(LEVEL/2) THEN PEDEDELAY=PEDEDELAY-1:IF PEDEDELAY<1 THEN PEDEDELAY=1
    NORMALDELAY=PEDEDELAY
    REM LET PLAYER SEE THEIR CURRENT SCORE
    LOCATE 12,24:COLOR 7,0:PRINT "CURRENT SCORE ";:COLOR 1,0
    NUMBER1=SCORE:GOSUB DRAWNUMBERPADDED
    REM FLASH CONTINUE MESSAGE UNTIL THE PLAYER CLICKS FIRE BUTTON
    CLEARJOY:IF JOY(JNUM)<>0 THEN CLEARJOY
    WAITFORKEY:
    MSGCOUNT=MSGCOUNT+1:IF MSGCOUNT>10 THEN MSGCOUNT=0:LOCATE 8,27:PRINT "WAVE COMPLETED"
    LOCATE 23,24:COLOR 11,3
    IF FLASHCOUNT=40 AND FLASH=0 THEN FLASHCOUNT=0:FLASH=1:PRINT "PRESS FIRE WHEN READY"
    IF FLASHCOUNT=40 AND FLASH=1 THEN FLASHCOUNT=0:FLASH=0:PRINT RPT$(32,30)
    FLASHCOUNT=FLASHCOUNT+1:COLOR CL,0:CL=CL+1:IF CL>15 THEN CL=0 
    J=JOY(JNUM):IF J<$40 THEN GOTO WAITFORKEY
    SCREEN 11
    GOTO STARTLEVEL


REM ---------------------------------------------
REM - LEVEL LOST
REM ---------------------------------------------
LEVELLOST:
    REM EXPLODE PLAYER
    PLAYERX=PEEK($26):PLAYERY=PEEK($27)
    TILE PLAYERX,PLAYERY,EXPLODECHAR,EXPLODECOLOR
    REM PLAY COOL SOUND
    FOR S=500 TO 100 STEP -1:PSGFREQ 0,S*2:PSGFREQ 0,S:NEXT:PSGFREQ 0,0
    REM LOSE A LIFE
    LIVES=LIVES-1:GOSUB DRAWLIVES
    REM CHECK FOR NEW LIFE AWARDED
    GOSUB NEWLIFE
    REM REMOVE EVERYTHING THAT MOVES
    GOSUB CLEARASSETS:GOSUB REMOVEPEDES    
    REM GIVE A LITTLE PAUSE
    SLEEP 60
    REM RETURN PETSCIIPEDES TO NORMAL SPEED
    PEDEDELAY=NORMALDELAY
    REM IS THIS GAME OVER OR ARE LIVES LEFT?
    IF LIVES<1 THEN ENDGAME
    GOTO STARTLEVEL



REM ---------------------------------------------
REM - ENDGAME
REM ---------------------------------------------
ENDGAME:
    IF SCORE>HIGHSCORE THEN HIGHSCORE=SCORE
    GOTO STARTGAME


REM ---------------------------------------------
REM 
REM - DISPLAY THE SPLASH SCREEN 
REM 
REM ---------------------------------------------
SPLASH:    
    COLOR 5,5:SCREEN 9:LOCATE 5,12:GOSUB SHOWTITLE
    LOCATE 11,23:COLOR 7,12:PRINT"FOR THE COMMANDER X16"
    IF HIGHSCORE=0 THEN SHOWACTORS
    LOCATE 16,20:COLOR 15,0
    PRINT "CURRENT HIGH SCORE  ";:NUMBER1=HIGHSCORE:COLOR 1,0:GOSUB DRAWNUMBERPADDED
    GOTO SETUPWAIT

    SHOWACTORS:
    LOCATE 14,23:COLOR 15,0:PRINT "  OBJECT":TILE 32,15,ENEMYBODYCHAR,COLORSPEDE(1)
    LOCATE 14,38:COLOR 15,0:PRINT "POINTS"
    LOCATE 16,23:COLOR 15,0:PRINT "    BODY":TILE 32,15,ENEMYBODYCHAR,COLORSPEDE(1)
    LOCATE 16,38:COLOR 1,0:PRINT "10"
    LOCATE 17,23:COLOR 15,0:PRINT "    HEAD":TILE 32,16,ENEMYBODYCHAR+2,COLORSPEDE(1)
    LOCATE 17,38:COLOR 1,0:PRINT "100"
    LOCATE 18,23:COLOR 15,0:PRINT "  SPIDER":TILE 32,17,SPIDERCHAR,COLORSPEDE(1)
    LOCATE 18,38:COLOR 1,0:PRINT "250"
    LOCATE 19,23:COLOR 15,0:PRINT "    FLEA":TILE 32,18,FLEACHAR,COLORSPEDE(1)
    LOCATE 19,38:COLOR 1,0:PRINT "200"
    LOCATE 20,23:COLOR 15,0:PRINT "MUSHROOM":TILE 32,19,MUSHROOMCHAR,COLORSPEDE(1)
    LOCATE 20,38:COLOR 1,0:PRINT "5"

    SETUPWAIT:
    FOR S=2500 TO 20 STEP -2:PSGFREQ 0,S*8:PSGFREQ 0,S:NEXT:PSGFREQ 0,0

    ALPHA1$="PRESS FIRE TO START GAME"
    GOSUB WAITFORUSER
    SCREEN 11
    RETURN



REM ---------------------------------------------
REM 
REM - DRAW THE PETSCIIPEDE TITLE
REM 
REM ---------------------------------------------
SHOWTITLE:    
    PRINT TAB(13);"{RVS ON}   {RIGHT}   {RIGHT}   {RIGHT}   {RIGHT}   {RIGHT} {RIGHT} {RIGHT}   {RIGHT}   {RIGHT}  {RIGHT}{RIGHT}   {RVS OFF}"
    PRINT TAB(13);"{RVS ON} {RIGHT} {RIGHT} {RIGHT}{RIGHT}{RIGHT}{RIGHT} {RIGHT}{RIGHT} {RIGHT}{RIGHT}{RIGHT} {RIGHT}{RIGHT}{RIGHT} {RIGHT} {RIGHT} {RIGHT} {RIGHT} {RIGHT}{RIGHT}{RIGHT} {RIGHT} {RIGHT} {RIGHT}{RIGHT}{RVS OFF}"
    PRINT TAB(13);"{RVS ON}   {RIGHT}  {RIGHT}{RIGHT}{RIGHT} {RIGHT}{RIGHT}   {RIGHT} {RIGHT}{RIGHT}{RIGHT} {RIGHT} {RIGHT}   {RIGHT}  {RIGHT}{RIGHT} {RIGHT} {RIGHT}  {RIGHT}{RVS OFF}"
    PRINT TAB(13);"{RVS ON} {RIGHT}{RIGHT}{RIGHT} {RIGHT}{RIGHT}{RIGHT}{RIGHT} {RIGHT}{RIGHT}{RIGHT}{RIGHT} {RIGHT} {RIGHT}{RIGHT}{RIGHT} {RIGHT} {RIGHT} {RIGHT}{RIGHT}{RIGHT} {RIGHT}{RIGHT}{RIGHT} {RIGHT} {RIGHT} {RIGHT}{RIGHT}{RVS OFF}"
    PRINT TAB(13);"{RVS ON} {RIGHT}{RIGHT}{RIGHT}   {RIGHT}{RIGHT} {RIGHT}{RIGHT}   {RIGHT}   {RIGHT} {RIGHT} {RIGHT} {RIGHT}{RIGHT}{RIGHT}   {RIGHT}  {RIGHT}{RIGHT}   {RVS OFF}"
    RETURN



REM ---------------------------------------------
REM - FLASH A MESSAGE AND 
REM - WAIT FOR USER TO PRESS FIRE
REM ---------------------------------------------
WAITFORUSER:
    CLEARKEYBUFF2:M=33-(LEN(ALPHA1$)/2):IF M<1 THEN M=1
    WAITFORFIRE:
    LOCATE 23,M:COLOR 11,3
    IF FLASHCOUNT=40 AND FLASH=0 THEN FLASHCOUNT=0:FLASH=1:PRINT ALPHA1$
    IF FLASHCOUNT=40 AND FLASH=1 THEN FLASHCOUNT=0:FLASH=0:PRINT RPT$(32,30)
    FLASHCOUNT=FLASHCOUNT+1
    J=JOY(JNUM):GET K$
    IF J<$40 THEN GOTO WAITFORFIRE
    RETURN

REM -------------------------------------
REM -
REM - DRAW SCORE, LEVEL, LIVES INFO
REM -
REM -------------------------------------
DRAWHUD:
    FOR X=1 TO 23:TILE 0,X,$67,FRAMECOLOR:TILE 31,X,$65,FRAMECOLOR:NEXT
    FOR X=1 TO 30:TILE X,24,$63,FRAMECOLOR:NEXT
    FOR X=1 TO 30:TILE X,1,$63,FRAMECOLOR:NEXT
    LOCATE 1,4:COLOR 0,5:REM COLOR IS LO BYTE/HI BYTE IN 256 COLOR
    PRINT"WAVE 00  SCORE 000000"
    GOSUB DRAWSCORE
    GOSUB DRAWLEVEL
    GOSUB DRAWLIVES
    RETURN


DRAWLEVEL:
    NUMBER2=LEN(STR$(LEVEL))-1
    LOCATE 1,9:COLOR 1,0:PRINT LEFT$("00",2-(NUMBER2));RIGHT$(STR$(LEVEL),NUMBER2)
    RETURN


DRAWLIVES:
    LOCATE 1,28:PRINT RPT$(32,3)
    IF LIVES>0 THEN LOCATE 1,28:COLOR 7,0:PRINT RPT$($7A,LIVES)
    RETURN


DRAWSCORE:
    LOCATE 1,19:NUMBER1=SCORE:COLOR 1,0:GOSUB DRAWNUMBERPADDED:RETURN

REM -------------------------------------
REM -
REM - DISPLAY A NUMBER PADDED WITH 6 0'S
REM -
REM -------------------------------------
DRAWNUMBERPADDED:
    NUMBER2=LEN(STR$(NUMBER1))-1
    PRINT LEFT$("000000",6-(NUMBER2));RIGHT$(STR$(NUMBER1),NUMBER2):RETURN



REM -------------------------------------
REM -
REM - ADDSCORE NEW LIFE
REM -
REM -------------------------------------
ADDPOINTS:    
    SCORE=SCORE+POINTS:WAVESCORE=WAVESCORE+POINTS:RETURN



REM -------------------------------------
REM -
REM - AWARD NEW LIFE IF ENOUGH POINTS
REM - HAVE BEEN SCORED
REM -
REM -------------------------------------
NEWLIFE:
    IF LIVES>=3 OR WAVESCORE<NEWLIFESCORE THEN RETURN
    LIVES=LIVES+1:C=1
    FOR S=100 TO 1500 STEP 8:PSGFREQ 0,S*2:PSGFREQ 0,S
        LOCATE 1,28:COLOR C,0:PRINT RPT$($7A,LIVES):C=C+1:IF C>15 THEN C=1
    NEXT:PSGFREQ 0,0:GOSUB DRAWLIVES
    RETURN



REM -------------------------------------
REM -
REM - INIT PEDES
REM -
REM -------------------------------------
INITPEDES:
    FOR B=0 TO 15
        POKE ENEMYX+B,0:POKE ENEMYY+B,0:POKE ENEMYBODY+B,0:POKE ENEMYDIR+B,1:POKE ENEMYROUTE+B,8
    NEXT
    SYS $0400
    RETURN



REM -------------------------------------
REM -
REM - REMOVE PEDES
REM -
REM -------------------------------------
REMOVEPEDES:
    FOR B=0 TO PEDELENMAX-1:TILE PEEK(ENEMYX+B),PEEK(ENEMYY+B),32:NEXT:RETURN



REM -------------------------------------
REM -
REM - MODIFY CHARACTER SET WITH GAME GFX
REM -
REM -------------------------------------
LOADCUSTOMCHARS:    
    CHDEF=CHARROM+(MUSHROOMCHAR*8):FOR X=CHDEF TO CHDEF+15:READ D:VPOKE 1,X,D:NEXT
    CHDEF=CHARROM+(ENEMYBODYCHAR*8):FOR X=CHDEF TO CHDEF+31:READ D:VPOKE 1,X,D:NEXT
    CHDEF=CHARROM+(PLAYERCHAR*8):FOR X=CHDEF TO CHDEF+7:READ D:VPOKE 1,X,D:NEXT
    CHDEF=CHARROM+(SPIDERCHAR*8):FOR X=CHDEF TO CHDEF+7:READ D:VPOKE 1,X,D:NEXT
    CHDEF=CHARROM+(FLEACHAR*8):FOR X=CHDEF TO CHDEF+7:READ D:VPOKE 1,X,D:NEXT
    CHDEF=CHARROM+(EXPLODECHAR*8):FOR X=CHDEF TO CHDEF+7:READ D:VPOKE 1,X,D:NEXT
    RETURN



REM -------------------------------------
REM -
REM - LOAD ML CODE INTO GOLDEN RAM
REM -
REM -------------------------------------    
LOADML:
    FOR M=$0400 TO $07B7:READ B:POKE M,B:NEXT
    RETURN



REM ----------------------------------------------------------------
REM -
REM - CUSTOMER CHARACTER DEFINITIONS
REM -
REM ----------------------------------------------------------------
REM MUSHROOM CHAR 1&2
DATA $00,$3C,$7E,$FF,$FF,$18,$18,$00,$00,$3C,$7E,$FF,$C3,$00,$00,$00
REM PETSCIIPEDE BODY 1&2
DATA $04,$08,$3E,$7F,$7F,$3E,$08,$04,$10,$08,$3E,$7F,$7F,$3E,$08,$10
REM PETSCIIPEDE HEAD 1&2
DATA $18,$0E,$BF,$7F,$7F,$BF,$0E,$18,$18,$70,$FD,$FE,$FE,$FD,$70,$18
REM PLAYER AND SPIDER
DATA $18,$18,$3C,$BD,$FF,$7E,$3C,$18,$00,$00,$81,$DB,$BD,$99,$81,$00
REM FLEA AND EXPLODE
DATA $00,$70,$FC,$FF,$54,$A2,$A0,$00,$92,$54,$00,$C6,$00,$44,$92,$10



REM ----------------------------------------------------------------
REM -
REM - MACHINE LANGUAGE PORTION OF THE GAME CODE
REM -
REM ----------------------------------------------------------------
DATA $4C,$89,$05,$4C,$D8,$05,$4C,$FF,$06,$4C,$2B,$07,$4C,$3B,$07,$4C
DATA $12,$04,$A5,$23,$F0,$0D,$20,$77,$04,$20,$4E,$04,$A5,$23,$F0,$03
DATA $20,$60,$04,$E6,$38,$A5,$37,$C5,$38,$D0,$22,$64,$38,$A9,$01,$C5
DATA $24,$D0,$0B,$20,$1D,$05,$20,$87,$04,$20,$EE,$04,$80,$0F,$A9,$01
DATA $C5,$25,$D0,$09,$20,$79,$05,$20,$2D,$05,$20,$3E,$05,$60,$A5,$29
DATA $C9,$02,$F0,$06,$3A,$85,$29,$4C,$5F,$04,$64,$23,$20,$77,$04,$60
DATA $A5,$28,$85,$70,$A5,$29,$85,$71,$20,$9C,$07,$85,$2F,$86,$30,$A9
DATA $5D,$A2,$3B,$20,$7C,$07,$60,$A5,$28,$85,$70,$A5,$29,$85,$71,$A5
DATA $2F,$A6,$30,$20,$7C,$07,$60,$A5,$2C,$29,$01,$D0,$09,$A5,$2C,$29
DATA $02,$D0,$0F,$4C,$ED,$04,$A5,$2A,$C9,$01,$F0,$4C,$3A,$85,$2A,$4C
DATA $AB,$04,$A5,$2A,$C9,$1E,$F0,$40,$1A,$85,$2A,$A5,$2C,$29,$08,$D0
DATA $20,$A5,$2C,$29,$04,$D0,$03,$4C,$ED,$04,$A5,$2B,$C9,$11,$F0,$06
DATA $3A,$85,$2B,$4C,$ED,$04,$A5,$2C,$49,$04,$09,$08,$85,$2C,$4C,$ED
DATA $04,$A5,$2B,$C9,$17,$F0,$06,$1A,$85,$2B,$4C,$ED,$04,$A5,$2C,$49
DATA $08,$09,$04,$85,$2C,$4C,$ED,$04,$64,$24,$20,$1D,$05,$60,$A5,$24
DATA $F0,$2A,$A5,$2A,$85,$70,$A5,$2B,$85,$71,$20,$9C,$07,$C9,$5A,$D0
DATA $04,$A2,$11,$86,$40,$C9,$58,$F0,$06,$C9,$59,$F0,$02,$A9,$20,$85
DATA $31,$86,$32,$E6,$35,$A9,$55,$A6,$35,$20,$7C,$07,$60,$A5,$2A,$85
DATA $70,$A5,$2B,$85,$71,$A5,$31,$A6,$32,$20,$7C,$07,$60,$A5,$2E,$C9
DATA $17,$F0,$05,$1A,$85,$2E,$80,$05,$64,$25,$20,$79,$05,$60,$A5,$25
DATA $F0,$36,$A5,$2D,$85,$70,$A5,$2E,$85,$71,$20,$9C,$07,$C9,$5A,$D0
DATA $07,$A9,$11,$85,$40,$4C,$78,$05,$C9,$58,$F0,$08,$C9,$59,$F0,$0B
DATA $A9,$20,$80,$07,$A9,$58,$A2,$F1,$20,$7C,$07,$85,$33,$86,$34,$E6
DATA $36,$A9,$5E,$A6,$36,$20,$7C,$07,$60,$A5,$2D,$85,$70,$A5,$2E,$85
DATA $71,$A5,$33,$A6,$34,$20,$7C,$07,$60,$20,$CF,$FE,$29,$1F,$C9,$13
DATA $90,$03,$3A,$80,$F9,$C9,$06,$B0,$04,$1A,$4C,$95,$05,$AA,$A0,$00
DATA $A9,$53,$99,$20,$A0,$8A,$99,$00,$A0,$A9,$02,$99,$10,$A0,$A9,$01
DATA $99,$30,$A0,$A9,$08,$99,$40,$A0,$C8,$E8,$8A,$99,$00,$A0,$A9,$02
DATA $99,$10,$A0,$98,$29,$01,$F0,$04,$A9,$51,$80,$02,$A9,$52,$99,$20
DATA $A0,$C8,$E8,$C4,$3C,$90,$E3,$60,$A4,$3C,$88,$B9,$20,$A0,$D0,$03
DATA $4C,$95,$06,$20,$B4,$06,$B9,$20,$A0,$C9,$53,$F0,$17,$C9,$54,$F0
DATA $13,$88,$B9,$00,$A0,$48,$B9,$10,$A0,$C8,$99,$10,$A0,$68,$99,$00
DATA $A0,$4C,$95,$06,$B9,$30,$A0,$AA,$29,$01,$D0,$0B,$8A,$29,$02,$D0
DATA $41,$20,$C6,$06,$4C,$95,$06,$B9,$00,$A0,$C9,$01,$F0,$67,$3A,$85
DATA $70,$B9,$10,$A0,$85,$71,$20,$9C,$07,$C9,$20,$F0,$11,$20,$9E,$06
DATA $C9,$5A,$D0,$07,$A9,$11,$85,$40,$4C,$3E,$06,$4C,$85,$06,$B9,$00
DATA $A0,$3A,$99,$00,$A0,$4C,$95,$06,$B9,$00,$A0,$1A,$99,$00,$A0,$4C
DATA $95,$06,$B9,$00,$A0,$C9,$1E,$F0,$1C,$1A,$85,$70,$B9,$10,$A0,$85
DATA $71,$20,$9C,$07,$C9,$20,$F0,$E0,$20,$9E,$06,$C9,$5A,$D0,$06,$A9
DATA $11,$85,$40,$80,$D3,$A9,$01,$99,$30,$A0,$A9,$53,$99,$20,$A0,$20
DATA $C6,$06,$4C,$95,$06,$A9,$02,$99,$30,$A0,$A9,$54,$99,$20,$A0,$20
DATA $C6,$06,$4C,$95,$06,$C0,$00,$F0,$04,$88,$4C,$DB,$05,$60,$C9,$58
DATA $D0,$11,$E0,$F1,$D0,$0D,$E6,$39,$A2,$02,$86,$40,$48,$A9,$08,$99
DATA $30,$A0,$68,$60,$B9,$00,$A0,$85,$70,$B9,$10,$A0,$85,$71,$A9,$20
DATA $A2,$00,$20,$7C,$07,$60,$B9,$40,$A0,$AA,$B9,$10,$A0,$E0,$04,$D0
DATA $08,$C5,$40,$F0,$11,$3A,$4C,$FB,$06,$E0,$08,$B0,$02,$80,$1F,$C9
DATA $17,$B0,$0A,$1A,$80,$15,$A9,$08,$99,$40,$A0,$80,$11,$A9,$04,$99
DATA $40,$A0,$B9,$10,$A0,$3A,$99,$10,$A0,$80,$03,$99,$10,$A0,$60,$A5
DATA $3C,$85,$76,$A0,$00,$A5,$3E,$85,$73,$B9,$00,$A0,$85,$70,$B9,$10
DATA $A0,$85,$71,$B9,$20,$A0,$85,$72,$A6,$73,$C9,$00,$F0,$03,$20,$7C
DATA $07,$C6,$76,$A5,$76,$F0,$03,$C8,$80,$DF,$60,$A5,$26,$85,$70,$A5
DATA $27,$85,$71,$A9,$5A,$A2,$50,$20,$7C,$07,$60,$A5,$26,$85,$70,$A5
DATA $27,$85,$71,$A9,$20,$A2,$00,$20,$7C,$07,$60,$A9,$00,$85,$74,$A9
DATA $B0,$85,$75,$A5,$71,$0A,$AA,$F0,$10,$18,$A5,$74,$69,$80,$85,$74
DATA $A5,$75,$69,$00,$85,$75,$CA,$D0,$F0,$A5,$70,$0A,$85,$77,$18,$A5
DATA $74,$65,$77,$85,$74,$A5,$75,$69,$00,$85,$75,$60,$48,$DA,$20,$4B
DATA $07,$9C,$25,$9F,$A5,$74,$8D,$20,$9F,$A5,$75,$8D,$21,$9F,$A9,$11
DATA $8D,$22,$9F,$FA,$68,$8D,$23,$9F,$8E,$23,$9F,$60,$20,$4B,$07,$9C
DATA $25,$9F,$A5,$74,$8D,$20,$9F,$A5,$75,$8D,$21,$9F,$A9,$11,$8D,$22
DATA $9F,$AD,$23,$9F,$AE,$23,$9F,$60
